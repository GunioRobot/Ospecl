#!/usr/bin/env ocaml

(**
 * Usage: $ ospecl module1_spec.ml module2_spec.ml ...
 * The arguments to ospecl should be source files which define a single value `specs : Spec.t list`.
 * The specs from each file will be executed in order and the overall results reported.
 *)

#use "topfind"
#require "unix"
#require "ospecl"

open Printf

let specs_of_file filename =
  let dir = Filename.dirname filename in
  Topdirs.dir_directory dir;
  let used = Toploop.use_file Format.std_formatter filename in
  if used then
    Obj.obj (Toploop.getvalue "specs")
  else
    failwith (sprintf "Could not use file %s" filename)

let _ =
  let usage_message = 
    let this_file = (Filename.basename Sys.argv.(0)) in
    sprintf "$ %s [options] [spec files]\n" this_file in

  let files = ref [] in
  let include_dirs = ref [] in
  let append_to list_ref value =
    list_ref := !list_ref @ [value]
  in

  let runner = ref Ospecl.Console.progress in
  let formatters = [
    ("p", Ospecl.Console.progress);
    ("progress", Ospecl.Console.progress);
    ("d", Ospecl.Console.documentation);
    ("doc", Ospecl.Console.documentation);
    ("documentation", Ospecl.Console.documentation);
  ] 
  in
  let choose_format f =
    runner := List.assoc f formatters
  in

  Arg.parse [
      ("-I", Arg.String (append_to include_dirs), "specify include directory");
      ("-format", Arg.Symbol (fst (List.split formatters), choose_format), " choose a formatter");
    ]
    (append_to files)
    usage_message;

  List.iter Topdirs.dir_directory !include_dirs;
  let all_specs = List.concat (List.map specs_of_file !files) in
  !runner all_specs
